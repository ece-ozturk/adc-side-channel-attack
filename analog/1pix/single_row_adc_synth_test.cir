Simulation of digital circuit synthesized with Yosys

** opencircuitdesign pdks install
.lib "/foss/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice" tt
.include "/foss/pdk/sky130A/libs.ref/sky130_fd_sc_hd/spice/sky130_fd_sc_hd.spice"
.include counter_synth.model
.include register_array_synth.model
.include negedge_detector_synth.model

* Instantiate counter module
XCOUNTER clk reset count.0 count.1 count.2 count.3 count.4 count.5 count.6 count.7 vss vss vdd vdd counter

* Instantiate edge detector module (detects negative edges of comparator signals)
XEDGE_DETECTOR clk reset comp.0_buffered enable vss vss vdd vdd negedge_detector

* Instantiate registers module (stores values based on edge detector output)
XREGISTER_ARRAY clk reset enable count.0 count.1 count.2 count.3 count.4 count.5 count.6 count.7 stored_values.0 stored_values.1 stored_values.2 stored_values.3 stored_values.4 stored_values.5 stored_values.6 stored_values.7 vss vss vdd vdd register_array

* ! Synthesizer optimizes the inversion in Verilog input. Inverters added manually for now.
XINV1 comp.0 vss vss vdd vdd comp.0_1 sky130_fd_sc_hd__inv_1
XINV2 comp.0_1 vss vss vdd vdd comp.0_2 sky130_fd_sc_hd__inv_1
XINV3 comp.0_2 vss vss vdd vdd comp.0_3 sky130_fd_sc_hd__inv_1
XINV4 comp.0_3 vss vss vdd vdd comp.0_4 sky130_fd_sc_hd__inv_1
XINV5 comp.0_4 vss vss vdd vdd comp.0_5 sky130_fd_sc_hd__inv_1
XINV6 comp.0_5 vss vss vdd vdd comp.0_6 sky130_fd_sc_hd__inv_1
XINV7 comp.0_6 vss vss vdd vdd comp.0_7 sky130_fd_sc_hd__inv_1
XINV8 comp.0_7 vss vss vdd vdd comp.0_buffered sky130_fd_sc_hd__inv_1

* Voltage sources
.param vdd=1.8
Vdd vdd 0 {vdd}

.param vss=0
Vss vss 0 {vss}

** Clock and Reset signals (1MHz clock)
Vrst    reset   0   pulse   {vdd}   {vss}   2u      10p     10p    490u    500u
Vclk    clk     0   pulse   {vss}   {vdd}   0       10p     10p     0.5u      1u

* Comparator signal (example: 1 comparators with random outputs)
Vcomp0 comp.0   0   pulse   {vss}   {vdd}   48u     200p    200p    0.5u    1u

** Simulation settings
*.option method=Gear
.option KLU
.option skywaterpdk

.control
set color0=white

tran 1n 60u
plot I(vdd)
plot V(comp.0)

set wr_singlescale
set wr_vecnames
wrdata outfiles/data -I(vdd)
.endc

.END